def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    compileSdkVersion project.compileSdkVersion as int
    buildToolsVersion project.buildToolsVersion

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId project.applicationId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(project.storeFile)
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled true //是否开启混淆，是否开启删除无用代码
            shrinkResources true //是否开启删除无用资源(minifyEnabled为true时设置有效)
            zipAlignEnabled true //是否开启资源文件通过内存映射对齐到4字节边界
            pseudoLocalesEnabled true //是否在APK中生成伪语言环境(国际化时用到)
            signingConfig signingConfigs.release //应用签名配置
            //混淆配置
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'libs', include: ['*.aar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.2.0"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // 协程(https://github.com/Kotlin/kotlinx.coroutines)
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"
    // google
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation 'com.google.android:flexbox:1.1.0'
    implementation "com.google.android.material:material:1.1.0"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "androidx.exifinterface:exifinterface:1.3.0-alpha01"

    // season (https://github.com/guangGG)
    implementation "gapp.season:EncryptLib:0.0.3"
    implementation 'gapp.season:bookreader:1.0.2'
    implementation 'gapp.season:skystar:1.0.0'
    //implementation(name: 'Poem', ext: 'aar')

    // reactive
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // network
    implementation "com.squareup.okhttp3:okhttp:3.12.6"
    implementation "com.amitshekhar.android:rx2-android-networking:1.0.2"

    // RecyclerView Adapter
    implementation "com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50"

    // mmkv
    implementation "com.tencent:mmkv:1.0.23"

    // database
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"

    // xpopup
    implementation "com.lxj:xpopup:1.8.8-x"
    // Barcode
    implementation "cn.bingoogolapple:bga-qrcode-zxing:1.3.7"
    // doraemonkit
    debugImplementation 'com.didichuxing.doraemonkit:doraemonkit:3.0.0'
    releaseImplementation 'com.didichuxing.doraemonkit:doraemonkit-no-op:3.0.0'


    // implementation in aar
    implementation "org.apache-extras.beanshell:bsh:2.0b6" //https://github.com/beanshell/beanshell
    implementation 'com.haibin:calendarview:3.6.7'
    implementation 'me.panavtec:drawableview:0.6.0'
    implementation 'com.github.piasy:BigImageViewer:1.5.7' //1.6.0的包会出现闪烁bug，不需要重复引入GlideLoader工具包
    // 生物识别(最低兼容到API-14)
    implementation "androidx.biometric:biometric:1.0.1"
    // GSYVideoPlayer(https://github.com/CarGuo/GSYVideoPlayer)
    //implementation 'com.shuyu:GSYVideoPlayer:7.1.1'
    implementation 'com.shuyu:gsyVideoPlayer-java:7.1.1'
    //implementation 'com.shuyu:GSYVideoPlayer-exo2:7.1.1'
    implementation 'com.shuyu:gsyVideoPlayer-ex_so:7.1.1'
}
